#If a cell in a grid has a probable and no other cell in the same grid has that same probable(counter for that grid and digit is 1)
#then fix the value of the cell.
sp {Sudoku*propose*one_probable_grid
   (state <state> ^name Sudoku)
   (<state> ^cell <cell>)   
   (<cell> ^grid <grid>  ^value 0 ^probable <probable>)
   (<state> ^counter <grid_counter>)
   (<grid_counter> ^grid <grid> ^digit <probable> ^count 1)

-->

   (<state> ^operator <operator> + < =)
   (<operator> ^name one_probable_grid ^cell <cell> ^probable <probable>)

}

sp {Sudoku*applyone_probable_grid
   (state <state> ^operator <operator>)
   (<operator> ^name one_probable_grid ^cell <cell> ^probable <probable>)

   (<state> ^cell <cell>)
   (<cell> ^row <row> ^col <col>  ^value 0 ^probable <probable>)

   
-->
   
   (<cell> ^value 0 -)
   (<cell> ^value <probable>)
   (write (crlf) |one_probable_grid fired: value of cell|<row>||<col>| is |<probable>| |)
}